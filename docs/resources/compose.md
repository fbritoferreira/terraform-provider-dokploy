---
page_title: "dokploy_compose Resource - dokploy"
subcategory: ""
description: |-
  Manages a Docker Compose stack in Dokploy. Allows deploying multi-container applications using docker-compose files.
---

# dokploy_compose (Resource)

Manages a Docker Compose stack in Dokploy. This resource allows you to deploy multi-container applications using docker-compose files, either from inline content or from a Git repository.

## Example Usage

### Inline Compose File

```terraform
resource "dokploy_project" "myproject" {
  name = "My Stack"
}

resource "dokploy_environment" "production" {
  name       = "Production"
  project_id = dokploy_project.myproject.id
}

resource "dokploy_compose" "wordpress" {
  name           = "wordpress-stack"
  project_id     = dokploy_project.myproject.id
  environment_id = dokploy_environment.production.id
  source_type    = "raw"
  deploy_on_create = true
  
  compose_file_content = <<-EOT
    version: '3.8'
    services:
      wordpress:
        image: wordpress:latest
        ports:
          - "8080:80"
        environment:
          WORDPRESS_DB_HOST: db
          WORDPRESS_DB_USER: wordpress
          WORDPRESS_DB_PASSWORD: wordpress
          WORDPRESS_DB_NAME: wordpress
        depends_on:
          - db
      db:
        image: mysql:8.0
        environment:
          MYSQL_DATABASE: wordpress
          MYSQL_USER: wordpress
          MYSQL_PASSWORD: wordpress
          MYSQL_ROOT_PASSWORD: rootpassword
        volumes:
          - db_data:/var/lib/mysql
    volumes:
      db_data:
  EOT
}
```

### From Git Repository

```terraform
resource "dokploy_project" "myproject" {
  name = "My Stack"
}

resource "dokploy_environment" "production" {
  name       = "Production"
  project_id = dokploy_project.myproject.id
}

resource "dokploy_compose" "app" {
  name             = "myapp-stack"
  project_id       = dokploy_project.myproject.id
  environment_id   = dokploy_environment.production.id
  source_type      = "git"
  custom_git_url   = "https://github.com/myorg/myapp.git"
  custom_git_branch = "main"
  compose_path     = "docker-compose.yml"
  auto_deploy      = true
  deploy_on_create = true
}
```

### From Private Git Repository with SSH Key

```terraform
resource "dokploy_ssh_key" "deploy_key" {
  name        = "deploy-key"
  description = "Deployment SSH key"
  public_key  = file("~/.ssh/id_rsa.pub")
  private_key = file("~/.ssh/id_rsa")
}

resource "dokploy_project" "myproject" {
  name = "Private Stack"
}

resource "dokploy_environment" "production" {
  name       = "Production"
  project_id = dokploy_project.myproject.id
}

resource "dokploy_compose" "private_app" {
  name                  = "private-app"
  project_id            = dokploy_project.myproject.id
  environment_id        = dokploy_environment.production.id
  source_type           = "git"
  custom_git_url        = "git@github.com:myorg/private-repo.git"
  custom_git_branch     = "main"
  custom_git_ssh_key_id = dokploy_ssh_key.deploy_key.id
  compose_path          = "docker-compose.prod.yml"
  auto_deploy           = true
  deploy_on_create      = true
}
```

### Deploy to Remote Server

```terraform
resource "dokploy_server" "worker" {
  name             = "worker-server"
  ip_address       = "192.168.1.100"
  port             = 22
  username         = "root"
  ssh_key_id       = dokploy_ssh_key.server_key.id
  enable_docker    = true
}

resource "dokploy_project" "myproject" {
  name = "Remote Stack"
}

resource "dokploy_environment" "production" {
  name       = "Production"
  project_id = dokploy_project.myproject.id
}

resource "dokploy_compose" "remote_app" {
  name           = "remote-app"
  project_id     = dokploy_project.myproject.id
  environment_id = dokploy_environment.production.id
  server_id      = dokploy_server.worker.id
  source_type    = "raw"
  deploy_on_create = true
  
  compose_file_content = <<-EOT
    version: '3.8'
    services:
      app:
        image: nginx:alpine
        ports:
          - "80:80"
  EOT
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `environment_id` (String) The environment ID this compose stack belongs to.
- `name` (String) The name of the compose stack.
- `project_id` (String) The project ID this compose stack belongs to.

### Optional

- `auto_deploy` (Boolean) Enable automatic deployment when changes are pushed to the Git repository.
- `compose_file_content` (String) The content of the docker-compose file. Used when source_type is 'raw'.
- `compose_path` (String) Path to the docker-compose file within the repository. Defaults to 'docker-compose.yml'.
- `custom_git_branch` (String) Branch to use from the Git repository.
- `custom_git_ssh_key_id` (String) SSH key ID for accessing private Git repositories.
- `custom_git_url` (String) Git repository URL. Used when source_type is 'git'.
- `deploy_on_create` (Boolean) Trigger a deployment after creating the compose stack.
- `server_id` (String) Server ID to deploy the compose stack to. If not specified, deploys to the default server.
- `source_type` (String) The source type for the compose stack: 'raw' for inline content, 'git' for Git repository.

### Read-Only

- `id` (String) The ID of this resource.

## Import

Import is supported using the following syntax:

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can be used, for example:

```shell
# Compose stacks can be imported using their ID
terraform import dokploy_compose.wordpress "compose-id-123"
```
